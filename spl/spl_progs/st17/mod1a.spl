//PROCESS MANAGER MODULE


alias functionNum R1;
alias currentPID R2;

//Free User Area Page
if(functionNum == 2) then
	alias userAreaPage R3;
	userAreaPage = [PROCESS_TABLE + (currentPID * 16) + 11];
	
	//Release Page
	multipush(R1,R2);
	R1 = 2;
	R2 = userAreaPage;
	call MOD_2;
	multipop(R1,R2);
	
	return;
endif;

//Exit Process
if(functionNum == 3) then
	//Free Page Table
	multipush(R1,R2);
	R1 = 4;
	call MOD_1;
	multipop(R1,R2);
	
	//Free User Area Page
	multipush(R1,R2);
	R1 = 2;
	call MOD_1; 
	multipop(R1,R2);
	
	[PROCESS_TABLE + (currentPID * 16) + 4] = TERMINATED;
	return;
endif;

//Free Page Table
if(functionNum == 4) then
	
	alias ptbr R3;
	ptbr = [PROCESS_TABLE + (currentPID*16) + 14];
	
	[ptbr + 0] = -1;
	[ptbr + 1] = "0000";
	[ptbr + 2] = -1;
	[ptbr + 3] = "0000";
	
	alias iter R4;
	iter = 4;
	
	while(iter < 20) do
		if([ptbr + iter] != -1) then
		
			//Release Page
			multipush(R1,R2,R3,R4);
			R1 = 2;
			R2 = [ptbr + iter];
			call MOD_2;
			multipop(R1,R2,R3,R4);
		
			[ptbr + iter] = -1;
			[ptbr + iter + 1] = "0000";
		endif;
		
		iter = iter + 2;
		
	endwhile;
	
	
	return;
	
endif;
